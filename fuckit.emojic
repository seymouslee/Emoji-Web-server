💭 emojicodec server.emojic
💭 GET / HTTP/1.1

📦 sockets 🏠
📦 files 🏠

🏁 🍇

    🔤HTTP/1.1 200 OK❌r❌n❌r❌n🔤 ➡️  sucessMsg
    🔤HTTP/1.1 404 Not Found❌r❌n❌r❌n🔤 ➡️  errorMsg

    🍺📇🐇📄 🔤webroot/error.html🔤❗️➡️ errorpage

    💭 initialise a port that listens to port 8888
    🍺🆕🏄 8888❗️ ➡️ server

    💭 while true
    🔁 👍 🍇

        🍺 🙋 server❗️ ➡️ clientSocket

        🔁 👍 🍇
            🆗 data 👂 clientSocket 1000❗️ 🍇

                😀 🔤🧲🍺 🔡data❗️🧲🔤❗️
                
                💭 split data into list
                🔫 🍺🔡data❗️ 🔤 🔤 ❗️ ➡️ request_list

                ↪️ 📏request_list❓◀️🙌 3 🍇
                    🔤🧲 🔡errorMsg❗️🧲🧲🍺 🔡errorpage❗️🧲🔤 ➡️ response
                    😀 🔤🧲 🔡 response❗️🧲🔤❗️
                    😀🔤Check your request headers🔤❗️
                🍉
                🙅 🍇

                    💭 assign list values to variables
                    🐽 request_list 1❗️ ➡️ requested_file

                    💭 preparing file directory
                    🔤webroot🧲requested_file🧲🔤 ➡️ 🖍🆕file_location

                    💭 default directory points to webroot/index.html
                    ↪️ file_location 🙌 🔤webroot/🔤🍇
                        🔤🧲file_location🧲index.html🔤 ➡️ 🖍file_location
                    🍉

                    😀 🔤🧲file_location🧲🔤❗️

                    🍺📇🐇📄 🔤🧲file_location🧲🔤❗️➡️ webpage

                    🔤🧲 🔡sucessMsg❗️🧲🧲🍺 🔡webpage❗️🧲🔤 ➡️ response
                    😀 🔤🧲 🔡 response❗️🧲🔤❗️


                    🆗 💬 clientSocket 📇 response ❗️❗️🍇
                    🍉
                    🙅 error 🍇
                        😀 🔤An error occured: 🧲💬error❗️🧲🔤❗️
                        🆗 💬 clientSocket 📇 errorMsg ❗️❗️🍇
                        🍉
                        🙅 error 🍇
                            💭😀 🔤An error occured: 🧲💬error❗️🧲🔤❗️
                        🍉
                    🍉
                🍉
            🍉
            🙅‍♀️ error 🍇
                😀 🔤An error occured: 🧲💬error❗️🧲🔤❗️
            🍉
        🍉 
    🍉
🍉